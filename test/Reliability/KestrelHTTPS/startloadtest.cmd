REM usage: startloadtest.cmd [Server:port] [NumConcurrentClients] [NumIterations]
REM Make sure Node.js is installed first

set SERVER_URI=%1
set NUM_CLIENTS=%2
set NUM_ITERATIONS=%3
set /A NUM_REQ_PER_ITERATION=10 * NUM_CLIENTS
set TEST_DATA_TEXT_FILE=testdatatext.data
set TEST_DATA_MULTIPART_FILE=testdata_multipartform.data
set NEW_MULTIPART_FORM_BOUNDARY=---MultiPartFormBoundary
cmd /c npm install -g loadtest

ECHO. > %TEST_DATA_TEXT_FILE%
ECHO. > %TEST_DATA_MULTIPART_FILE%
CALL :GenerateTestTextData 50 %TEST_DATA_TEXT_FILE%
CALL :GenerateTestDataMultiPartForm 100 10 %TEST_DATA_MULTIPART_FILE%
CALL :RunTest
GOTO :EOF


:RunTest
SET /A LOOPIDX=0
:START_TEST
cmd /c runtestscenarios.cmd %SERVER_URI% %NUM_CLIENTS%
SET /A LOOPIDX=LOOPIDX+1
if %LOOPIDX% GEQ %NUM_ITERATIONS% goto :EOF
GOTO :START_TEST


:GenerateTestTextData REM %1=LOOPCOUNT %2=FILENAME

SET /A LOOPIDX=0
SET /A LOOPCOUNT=%1
SET TESTDATA_CONTENT=01234567890123456789012345678901234567890012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789

:GENFILELOOP
if %LOOPIDX%==0 (
    ECHO content=>> %2
) else (
    ECHO content%LOOPIDX%=>> %2
)
ECHO %TESTDATA_CONTENT%  >> %2
ECHO ^&  >> %2
SET /A LOOPIDX=LOOPIDX+1
if %LOOPIDX% GEQ %LOOPCOUNT% goto :EOF
GOTO :GENFILELOOP



:GenerateTestDataMultiPartForm REM %1=LOOPCOUNT %2=BLOCKLOOPCOUNT %3=FILENAME

SET /A LOOPIDX=0
SET /A LOOPCOUNT=%1
:GENFILELOOP_MULTIPART_FORM
ECHO --%NEW_MULTIPART_FORM_BOUNDARY%>>%3
if %LOOPIDX%==0 (
    ECHO Content-Disposition: form-data; name="content">> %3
) else (
    ECHO Content-Disposition: form-data; name="content%LOOPIDX%">> %3
)
ECHO.>> %3

SET /A BLOCKLOOPIDX=0
SET /A BLOCKLOOPCOUNT=%RANDOM% * %2 * 2 / 32768 + 1
:GEN_SECTIONBLOCK
ECHO %TESTDATA_CONTENT% >>%3
SET /A BLOCKLOOPIDX=BLOCKLOOPIDX+1
if %BLOCKLOOPIDX% GEQ %BLOCKLOOPCOUNT% goto :END_GEN_SECTIONBLOCK
goto :GEN_SECTIONBLOCK
:END_GEN_SECTIONBLOCK

SET /A LOOPIDX=LOOPIDX+1
if %LOOPIDX% GEQ %LOOPCOUNT% goto :END_GEN_MULTIPART_FORM
GOTO :GENFILELOOP_MULTIPART_FORM

:END_GEN_MULTIPART_FORM
ECHO --%NEW_MULTIPART_FORM_BOUNDARY%-->> %3
GOTO :EOF


:EOF